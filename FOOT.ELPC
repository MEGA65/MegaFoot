' Way of the Imploding Foot
' -------------------------
' A collaborative team-effort game made by the MEGA65 community
'
' Gurce:
' - Initial background art and Eleven code infrastructure

#output "foot"

' BANK4 Memory usage
' ------------------
' 4.0000 DMA-LIST
' 4.0100 SUNSET PALETTES (each palette is 16*3 in size, have 12 palettes)
' 4.0800 SCREEN MEMORY
' 4.2000 PETSCII GRAPHICS

' BANK5 Memory usage
' ------------------
' 5.0000 screen buffer
' 5.1000 color ram buffer
' 5.2000 doubled-up layer buffer

.petscii vars
'------------
#declare data_addr = $42000
#declare max_frames
#declare frame_addr(64)
#declare frame_dblup_addr(64)

#declare frm_idx = 0
#declare frmw, frmh,frmx,frmy,frmx1,frmy1,frameidx,cdata_addr
#declare yy, scraddr,clraddr
#declare a$, lidx, k, x, y
#declare src_addr, dest_addr, length, transp
#declare dest_mb_sel, dest_bank

#declare dblup_addr = $52000

#declare kneel_flag = 0

.imgs
'----
#define IMG_CLOUDS = 0
#define IMG_SKY = 1
#define IMG_LT_MOUNT = 2
#define IMG_DK_MOUNT = 3
#define IMG_WATER = 4
#define IMG_HI_GROUND = 5
#define IMG_LO_GROUND = 6
#define IMG_SUN = 7
#define IMG_PWALK1 = 8
#define IMG_PWALK2 = 9
#define IMG_PWALK3 = 10
#define IMG_PPUNCH1 = 11
#define IMG_PPUNCH2 = 12
#define IMG_PPUNCH3 = 13
#define IMG_PKICK1 = 14
#define IMG_PKICK2 = 15
#define IMG_PKICK3 = 16
#define IMG_PKNEEL1 = 17
#define IMG_PKNEEL2 = 18
#define IMG_PKNEEL3 = 19
#define IMG_PKPUNCH1 = 20
#define IMG_PKPUNCH2 = 21
#define IMG_PKPUNCH3 = 22
#define IMG_PKKICK1 = 23
#define IMG_PKKICK2 = 24
#define IMG_PKKICK3 = 25
#define IMG_PDIE1 = 26
#define IMG_PDIE2 = 27
#define IMG_PDIE3 = 28
#define IMG_PDIE4 = 29
#define IMG_PDIE5 = 30

#define IMG_DWALK1 = 31
#define IMG_DWALK2 = 32
#define IMG_DWALK3 = 33
#define IMG_DQUACK1 = 34
#define IMG_DQUACK2 = 35
#define IMG_DQUACK3 = 36
#define IMG_DDIE1 = 37
#define IMG_DDIE2 = 38
#define IMG_DDIE3 = 39
#define IMG_DDIE4 = 40
#define IMG_DDIE5 = 41


#define HIGHEST_LAYER = 7

#declare fx, fy

#declare xoffs(HIGHEST_LAYER) ' offset into cycling of graphic

#declare draw_order(HIGHEST_LAYER)
draw_order(0) = IMG_SKY
draw_order(1) = IMG_SUN
draw_order(2) = IMG_CLOUDS
draw_order(3) = IMG_LT_MOUNT
draw_order(4) = IMG_DK_MOUNT
draw_order(5) = IMG_WATER
draw_order(6) = IMG_HI_GROUND
draw_order(7) = IMG_LO_GROUND

#declare offs_amount(HIGHEST_LAYER)
offs_amount(0) = .7
offs_amount(1) = 0
offs_amount(2) = .1
offs_amount(3) = .5
offs_amount(4) = 1
offs_amount(5) = 1.5
offs_amount(6) = 2

#declare palidx, ptr

.anims
'-----
#define ANIM_PWALK = 0
#define ANIM_PPUNCH = 1
#define ANIM_PKICK = 2
#define ANIM_PKNEEL_DN = 3
#define ANIM_PKNEEL_UP = 4
#define ANIM_PKPUNCH = 5
#define ANIM_PKKICK = 6
#define ANIM_PDIE = 7

#define ANIM_DWALK = 8
#define ANIM_DQUACK = 9
#define ANIM_DDIE = 10

#define ANIM_MAX = 11


#declare anim_order(ANIM_MAX, 10)
anim_order(ANIM_PWALK, 0) = IMG_PWALK1
anim_order(ANIM_PWALK, 1) = IMG_PWALK2
anim_order(ANIM_PWALK, 2) = IMG_PWALK3
anim_order(ANIM_PWALK, 3) = IMG_PWALK2

anim_order(ANIM_PPUNCH, 0) = IMG_PPUNCH1
anim_order(ANIM_PPUNCH, 1) = IMG_PPUNCH2
anim_order(ANIM_PPUNCH, 2) = IMG_PPUNCH3
anim_order(ANIM_PPUNCH, 3) = IMG_PPUNCH2
anim_order(ANIM_PPUNCH, 4) = IMG_PPUNCH1

anim_order(ANIM_PKICK, 0) = IMG_PKICK1
anim_order(ANIM_PKICK, 1) = IMG_PKICK2
anim_order(ANIM_PKICK, 2) = IMG_PKICK3
anim_order(ANIM_PKICK, 3) = IMG_PKICK2
anim_order(ANIM_PKICK, 4) = IMG_PKICK1

anim_order(ANIM_PKNEEL_DN, 0) = IMG_PKNEEL1
anim_order(ANIM_PKNEEL_DN, 1) = IMG_PKNEEL2
anim_order(ANIM_PKNEEL_DN, 2) = IMG_PKNEEL3

anim_order(ANIM_PKNEEL_UP, 0) = IMG_PKNEEL2
anim_order(ANIM_PKNEEL_UP, 1) = IMG_PKNEEL1

anim_order(ANIM_PKPUNCH, 0) = IMG_PKPUNCH1
anim_order(ANIM_PKPUNCH, 1) = IMG_PKPUNCH2
anim_order(ANIM_PKPUNCH, 2) = IMG_PKPUNCH3
anim_order(ANIM_PKPUNCH, 3) = IMG_PKPUNCH2
anim_order(ANIM_PKPUNCH, 4) = IMG_PKPUNCH1

anim_order(ANIM_PKKICK, 0) = IMG_PKKICK1
anim_order(ANIM_PKKICK, 1) = IMG_PKKICK2
anim_order(ANIM_PKKICK, 2) = IMG_PKKICK3
anim_order(ANIM_PKKICK, 3) = IMG_PKKICK2
anim_order(ANIM_PKKICK, 4) = IMG_PKKICK1

anim_order(ANIM_PDIE, 0) = IMG_PDIE1
anim_order(ANIM_PDIE, 1) = IMG_PDIE2
anim_order(ANIM_PDIE, 2) = IMG_PDIE3
anim_order(ANIM_PDIE, 3) = IMG_PDIE4
anim_order(ANIM_PDIE, 4) = IMG_PDIE5
anim_order(ANIM_PDIE, 5) = IMG_PDIE5
anim_order(ANIM_PDIE, 6) = IMG_PDIE5
anim_order(ANIM_PDIE, 7) = IMG_PDIE5
anim_order(ANIM_PDIE, 8) = IMG_PDIE5

anim_order(ANIM_DWALK, 0) = IMG_DWALK1
anim_order(ANIM_DWALK, 1) = IMG_DWALK2
anim_order(ANIM_DWALK, 2) = IMG_DWALK3
anim_order(ANIM_DWALK, 3) = IMG_DWALK2

anim_order(ANIM_DQUACK, 0) = IMG_DQUACK1
anim_order(ANIM_DQUACK, 1) = IMG_DQUACK2
anim_order(ANIM_DQUACK, 2) = IMG_DQUACK3
anim_order(ANIM_DQUACK, 3) = IMG_DQUACK2
anim_order(ANIM_DQUACK, 4) = IMG_DQUACK1

anim_order(ANIM_DDIE, 0) = IMG_DDIE1
anim_order(ANIM_DDIE, 1) = IMG_DDIE2
anim_order(ANIM_DDIE, 2) = IMG_DDIE3
anim_order(ANIM_DDIE, 3) = IMG_DDIE4
anim_order(ANIM_DDIE, 4) = IMG_DDIE5
anim_order(ANIM_DDIE, 5) = IMG_DDIE5
anim_order(ANIM_DDIE, 6) = IMG_DDIE5
anim_order(ANIM_DDIE, 7) = IMG_DDIE5
anim_order(ANIM_DDIE, 8) = IMG_DDIE5


#declare max_frame(ANIM_MAX)
max_frame(ANIM_PWALK) = 4
max_frame(ANIM_PPUNCH) = 5
max_frame(ANIM_PKICK) = 5
max_frame(ANIM_PKNEEL_DN) = 3
max_frame(ANIM_PKNEEL_UP) = 2
max_frame(ANIM_PKPUNCH) = 5
max_frame(ANIM_PKKICK) = 5
max_frame(ANIM_PDIE) = 9

max_frame(ANIM_DWALK) = 4
max_frame(ANIM_DQUACK) = 5
max_frame(ANIM_DDIE) = 9

.fanim
#define FORM_HUMAN = 0
#define FORM_DUCK = 1
#declare player_form = 0 ' HUMAN

#define FANIM_WALK = 0
#define FANIM_PUNCH = 1
#define FANIM_KICK = 2
#define FANIM_KNEEL_DN = 3
#define FANIM_KNEEL_UP = 4
#define FANIM_KPUNCH = 5
#define FANIM_KKICK = 6
#define FANIM_DIE = 7

#define FANIM_MAX = 8

#declare form_anim(2, FANIM_MAX)
form_anim(FORM_HUMAN, FANIM_WALK) = ANIM_PWALK
form_anim(FORM_HUMAN, FANIM_PUNCH) = ANIM_PPUNCH
form_anim(FORM_HUMAN, FANIM_KICK) = ANIM_PKICK
form_anim(FORM_HUMAN, FANIM_KNEEL_DN) = ANIM_PKNEEL_DN
form_anim(FORM_HUMAN, FANIM_KNEEL_UP) = ANIM_PKNEEL_UP
form_anim(FORM_HUMAN, FANIM_KPUNCH) = ANIM_PKPUNCH
form_anim(FORM_HUMAN, FANIM_KKICK) = ANIM_PKKICK
form_anim(FORM_HUMAN, FANIM_DIE) = ANIM_PDIE

form_anim(FORM_DUCK, FANIM_WALK) = ANIM_DWALK
form_anim(FORM_DUCK, FANIM_PUNCH) = ANIM_DQUACK
form_anim(FORM_DUCK, FANIM_DIE) = ANIM_DDIE

#declare player_frame = 0
#declare player_anim = 0 ' ANIM_PWALK

'----
.main
'----
print chr$(27)+"5"
print chr$(147);
border 0
background 0
gosub load_data
palidx = 0
gosub set_palette

.loop0
  gosub init_vars
  gosub show_title
  gosub gameplay
  ' gosub gameover
  goto loop0

'---------------
.transparent_dma
'---------------
  wpoke $40009, src_addr
  wpoke $4000c, dest_addr
  wpoke $40007, length
  poke  $40004, transp
  poke $d702, 4 ' dma list in bank 4
  poke $d701, $00 ' dma list msb
  poke $d705, $00 ' dma list lsb

  return


'----------
.show_title
'----------
  print chr$(147);
  print "Way of the Imploding Foot"
  print "========================="
  print "A game made by the MEGA65 community!"
  get key a$
  a$=""
  return


'---------
.load_data
'---------
  ' FORMAT:
  ' ------
  ' - The same as used by Xanadu grab tool?
  ' - Save out a file for parallax layers
  ' - Save out a file for sprites

  bload "repaired.bin",p($42000)
  gosub init_petscii_frames
  gosub prepare_double_copy_layer

  bload "pal.bin",p($40100),r
  return

'-----------
.set_palette
'----------- (palidx)
  ptr = $40100 + palidx * 16 * 3

  for k = 0 to 15
    ' red
    poke $d100 + k + 16, peek(ptr)
    ptr = ptr + 1

    ' green
    poke $d200 + k + 16, peek(ptr)
    ptr = ptr + 1

    ' blue
    poke $d300 + k + 16, peek(ptr)
    ptr = ptr + 1
  next k
  return

'---------
.init_vars
'---------
  ' clear dma list area

  for k = 0 to 20
    poke $40000+k, 0
  next k

  poke $40000, $81 ' opt = dest addr MB selector
  poke $40001, $00 ' default to 1st megabyte

  poke $40002, $07 ' opt = enable transparency
  poke $40003, $86 ' opt = set transparency
  poke $40004, $20 ' transparency value
  poke $40005, $00 ' end of options
  poke $40006, $00 ' CMD lsb = COPY
  wpoke $40007, $0000 ' length
  wpoke $40009, $1000 ' source addr
  poke $4000b, $05    ' source bank
  wpoke $4000c, $1000 ' dest addr
  poke $4000e, $05    ' dest bank
  poke $4000f, $00    ' CMD msb (ignore)
  wpoke $40010, $0000 ' modulo (ignore)

  ' clear the offscreen buffer
  edma 3, $f00, 160, $50000
  edma 3, $a0, $20, $50f00
  edma 3, $fa0, $4f, $51000

  kneel_flag = 0
  player_form = FORM_HUMAN

  player_frame = 0
  player_anim = form_anim(player_form, FANIM_WALK)

  return

'-------------------
.init_petscii_frames
'-------------------
  max_frames = peek(data_addr)
  data_addr = data_addr + 1

  do while frm_idx < max_frames
    frame_addr(frm_idx) = data_addr
    data_addr = data_addr + 2
    frmw = peek(data_addr)
    data_addr = data_addr + 1
    frmh = peek(data_addr)
    data_addr = data_addr + 1
    data_addr = data_addr + frmw * frmh

    ':print chr$(147);"frm_idx=";frm_idx

    frm_idx = frm_idx + 1
  loop
  return


'-------------------------
.prepare_double_copy_layer
'-------------------------
  dblup_addr = $52000

  for frameidx = 0 to HIGHEST_LAYER
    data_addr = frame_addr(frameidx)
    frmx = peek(data_addr)
    data_addr = data_addr + 1
    frmy = peek(data_addr)
    data_addr = data_addr + 1
    frmw = peek(data_addr)
    data_addr = data_addr + 1
    frmh = peek(data_addr)
    data_addr = data_addr + 1

    frmx1=frmx+frmw-1
    frmy1=frmy+frmh-1

    frame_dblup_addr(frameidx) = dblup_addr

    ' COPY COLOR for each row twice into some location in dblup_addr
    for y = frmy to frmy1
      for k = 0 to 1
          ' copy it to some place in $5.2000
          edma 0, frmw, data_addr, dblup_addr
          dblup_addr = dblup_addr + frmw
      next k
      data_addr = data_addr + frmw
    next y

  next frameidx

  return


'---------------
.draw_background
'---------------
  gosub get_user_input

   '   frameidx = IMG_SUN
   '   gosub draw_petscii_layer 
   '   return

  poke $4000b, $05  ' assure source bank 5 (for doubled-up layers)

  for lidx = 0 to HIGHEST_LAYER
     ' if layer_visible[lidx] = 1 then begin
       frameidx = draw_order(lidx)
       gosub draw_petscii_layer 
     'bend
  next lidx

  gosub draw_player

  return


'--------------
.get_user_input
'--------------
  get a$

  ' handle no input cases
  ' ---------------------
  if a$="" and player_anim = ANIM_PKNEEL_DN then begin
    if player_frame >= max_frame(ANIM_PKNEEL_DN)-1 then begin
    kneel_flag = 0
    player_anim = ANIM_PKNEEL_UP
    player_frame = 0
    bend
  bend
  if a$="" then return

  ' handle cases with some input
  ' ----------------------------
  if a$="{x1D}" then gosub incr_offs:kneel_flag = 0
  if a$="{x9D}" then gosub decr_offs:kneel_flag = 0
  if a$="," and palidx>0 then palidx=palidx-1:gosub set_palette
  if a$="." and palidx<11 then palidx=palidx+1:gosub set_palette

  if a$=" " and player_anim = form_anim(player_form, FANIM_WALK) then begin
    player_anim = form_anim(player_form, FANIM_PUNCH)
    if kneel_flag = 1 then player_anim = ANIM_PKPUNCH
    player_frame = 0
  bend
  if a$="z" and player_anim = ANIM_PWALK then begin
    player_anim = ANIM_PKICK
    if kneel_flag = 1 then player_anim = ANIM_PKKICK
    player_frame = 0
  bend
  if a$="x" then begin
    kneel_flag = 0
    player_anim = form_anim(player_form, FANIM_DIE)
    player_frame = 0
  bend
  if a$="{x11}" and player_anim = ANIM_PWALK then begin
    player_anim = ANIM_PKNEEL_DN
    player_frame = 0
  bend

  ' d = toggle between human and duck
  if a$="d" then begin
    if player_form = FORM_HUMAN then begin
      player_form = FORM_DUCK
    bend:else begin
      player_form = FORM_HUMAN
    bend
    player_anim = form_anim(player_form, FANIM_WALK)
    player_frame = 0
    kneel_flag = 0
  bend

  return


'-----------
.draw_player
'-----------
  frameidx = anim_order(player_anim, player_frame)
  fx=0 : fy=23
  gosub draw_petscii_frame

  gosub consider_next_frame
  return


'-------------------
.consider_next_frame
'-------------------
  if player_anim = form_anim(player_form, FANIM_WALK) and (a$="{x9D}" or a$="{x1D}") then begin
    player_frame = player_frame + 1
  bend
  if player_anim <> form_anim(player_form, FANIM_WALK) then begin
    player_frame = player_frame + 1
  bend

  if player_anim = ANIM_PKNEEL_DN and a$="{x11}" then begin
    if player_frame >= max_frame(ANIM_PKNEEL_DN) then begin
      player_frame = max_frame(ANIM_PKNEEL_DN) - 1
      kneel_flag = 1
    bend
    return
  bend

  if player_frame >= max_frame(player_anim) then begin
    if player_anim = ANIM_PDIE then begin
      player_anim = ANIM_PKNEEL_UP
      player_frame = 0
      return
    bend
    if kneel_flag = 1 and player_anim <> ANIM_PKNEEL_DN then begin
      player_anim = ANIM_PKNEEL_DN
      player_frame = max_frame(ANIM_PKNEEL_DN) - 1
    bend:else begin
      player_anim = form_anim(player_form, FANIM_WALK)
      player_frame = 0
    bend
  bend

  return

'------------------
.draw_petscii_frame
'------------------
  poke $4000b, $04  ' assure source bank 4 (for original frame data)

  data_addr = frame_addr(frameidx)
  frmx = peek(data_addr)
  data_addr = data_addr + 1
  frmy = peek(data_addr)
  data_addr = data_addr + 1
  frmw = peek(data_addr)
  data_addr = data_addr + 1
  frmh = peek(data_addr)
  data_addr = data_addr + 1

  frmx = fx ' override position
  frmy = fy

  frmx1=frmx+frmw-1
  frmy1=frmy+frmh-1

  scraddr = $50000 + frmx + frmy * 80
  clraddr = $51000 + frmx + frmy * 80
  cdata_addr = data_addr

  for yy=frmy to frmy1

    ' draw chars
    ' ----------
    src_addr = cdata_addr
    dest_addr = clraddr
    length = frmw
    transp = $ff
    gosub transparent_dma

    cdata_addr = cdata_addr + frmw
    clraddr = clraddr + 80  
  next yy
  return


'------------------
.draw_petscii_layer
'------------------
  data_addr = frame_addr(frameidx)
  frmx = peek(data_addr)
  data_addr = data_addr + 1
  frmy = peek(data_addr)
  data_addr = data_addr + 1
  frmw = peek(data_addr)
  data_addr = data_addr + 1
  frmh = peek(data_addr)
  data_addr = data_addr + 1

  frmx1=frmx+frmw-1
  frmy1=frmy+frmh-1

  scraddr = $50000 + frmx + frmy * 80  ' todo - remove
  clraddr = $51000 + frmx + frmy * 80
  cdata_addr = data_addr + frmw * frmh

  xoffs(frameidx) = mod(xoffs(frameidx) + 80, 80)
  xoffs = int(xoffs(frameidx))

  cdata_addr = frame_dblup_addr(frameidx)

  for yy=frmy to frmy1

    ' draw chars from offset to end
    ' -----------------------------
    src_addr = cdata_addr + xoffs
    dest_addr = clraddr
    length = frmw
    transp = $ff
    gosub transparent_dma

    cdata_addr = cdata_addr + frmw * 2
    clraddr = clraddr + 80  
  next yy
  return


'--------
.gameplay
'--------
  gosub draw_background
  ' gosub draw_sprites
  gosub page_flip
  ' gosub user_input
  ' gosub game_logic
  goto gameplay
  return


'---------
.page_flip
'---------
  edma 0, $fa0, $50000, $40800
  edma 0, $fa0, $51000, $ff80000

  ' fill the top part
  edma 3, $f00, $4f, $51000
  return

'---------
.incr_offs
'---------
  for k = 0 to HIGHEST_LAYER
    xoffs(k) = xoffs(k) + offs_amount(k)
  next k
  return

'---------
.decr_offs
'---------
  for k = 0 to HIGHEST_LAYER
    xoffs(k) = xoffs(k) - offs_amount(k)
  next k
  return
ÿ