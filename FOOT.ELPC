' Way of the Imploding Foot
' -------------------------
' A collaborative team-effort game made by the MEGA65 community
'
' Gurce:
' - Initial background art and Eleven code infrastructure

#output "foot"

' BANK4 Memory usage
' ------------------
' 4.0000 DMA-LIST
' 4.0100 SUNSET PALETTES (each palette is 16*3 in size, have 12 palettes)
' 4.0800 SCREEN MEMORY
' 4.2000 PETSCII GRAPHICS

' BANK5 Memory usage
' ------------------
' 5.0000 screen buffer
' 5.1000 color ram buffer
' 5.2000 doubled-up layer buffer

.petscii vars
'------------
#declare data_addr = $42000
#declare max_frames
#declare frame_addr(64)
#declare frame_dblup_addr(64)

#declare frm_idx = 0
#declare frmw, frmh,frmx,frmy,frmx1,frmy1,frameidx,cdata_addr
#declare yy, scraddr,clraddr
#declare a$, lidx, k, x, y
#declare src_addr, dest_addr, length, transp
#declare dest_mb_sel, dest_bank

#declare dblup_addr = $52000

#define IMG_CLOUDS = 0
#define IMG_SKY = 1
#define IMG_LT_MOUNT = 2
#define IMG_DK_MOUNT = 3
#define IMG_WATER = 4
#define IMG_HI_GROUND = 5
#define IMG_LO_GROUND = 6
#define IMG_SUN = 7
#define IMG_PWALK1 = 8
#define IMG_PWALK2 = 9
#define IMG_PWALK3 = 10
#define IMG_PPUNCH1 = 11
#define IMG_PPUNCH2 = 12
#define IMG_PPUNCH3 = 13
#define IMG_PKICK1 = 14
#define IMG_PKICK2 = 15
#define IMG_PKICK3 = 16

#define HIGHEST_LAYER = 7

#declare fx, fy

#declare xoffs(HIGHEST_LAYER) ' offset into cycling of graphic

#declare draw_order(HIGHEST_LAYER)
draw_order(0) = IMG_SKY
draw_order(1) = IMG_SUN
draw_order(2) = IMG_CLOUDS
draw_order(3) = IMG_LT_MOUNT
draw_order(4) = IMG_DK_MOUNT
draw_order(5) = IMG_WATER
draw_order(6) = IMG_HI_GROUND
draw_order(7) = IMG_LO_GROUND

#declare offs_amount(HIGHEST_LAYER)
offs_amount(0) = .7
offs_amount(1) = 0
offs_amount(2) = .1
offs_amount(3) = .5
offs_amount(4) = 1
offs_amount(5) = 1.5
offs_amount(6) = 2

#declare palidx, ptr

#define ANIM_PWALK = 0
#define ANIM_PPUNCH = 1
#define ANIM_PKICK = 2

' #struct ANIMDETS, bars%, cpy%

' SONG songs(4) = [ {x5F}
'  [ 18, 6, 1 ], {x5F}
'  [ 25, 14, 1 ], {x5F}
'  [ 35, 10, 0 ], {x5F}
'  [ 15, 6, 1 ], {x5F}
'  [ 20, 9, 1 ]  {x5F}
']

#declare anim_order(3, 10)
anim_order(ANIM_PWALK, 0) = IMG_PWALK1
anim_order(ANIM_PWALK, 1) = IMG_PWALK2
anim_order(ANIM_PWALK, 2) = IMG_PWALK3
anim_order(ANIM_PWALK, 3) = IMG_PWALK2

anim_order(ANIM_PPUNCH, 0) = IMG_PPUNCH1
anim_order(ANIM_PPUNCH, 1) = IMG_PPUNCH2
anim_order(ANIM_PPUNCH, 2) = IMG_PPUNCH3
anim_order(ANIM_PPUNCH, 3) = IMG_PPUNCH2

anim_order(ANIM_PKICK, 0) = IMG_PKICK1
anim_order(ANIM_PKICK, 1) = IMG_PKICK2
anim_order(ANIM_PKICK, 2) = IMG_PKICK3
anim_order(ANIM_PKICK, 3) = IMG_PKICK2


#declare MAX_PFRAME = 4

#declare player_frame = 0
#declare player_anim = 0 ' ANIM_PWALK

'----
.main
'----
print chr$(27)+"5"
print chr$(147);
border 0
background 0
gosub load_data
palidx = 0
gosub set_palette

.loop0
  gosub init_vars
  gosub show_title
  gosub gameplay
  ' gosub gameover
  goto loop0

'---------------
.transparent_dma
'---------------
  wpoke $40009, src_addr
  wpoke $4000c, dest_addr
  wpoke $40007, length
  poke  $40004, transp
  poke $d702, 4 ' dma list in bank 4
  poke $d701, $00 ' dma list msb
  poke $d705, $00 ' dma list lsb

  return


'----------
.show_title
'----------
  print chr$(147);
  print "Way of the Imploding Foot"
  print "========================="
  print "A game made by the MEGA65 community!"
  return


'---------
.load_data
'---------
  ' FORMAT:
  ' ------
  ' - The same as used by Xanadu grab tool?
  ' - Save out a file for parallax layers
  ' - Save out a file for sprites

^^bload "layers.bin",p($42000),r
  gosub init_petscii_frames
  gosub repair_petscii_frames
  gosub prepare_double_copy_layer

^^bload "pal.bin",p($40100),r
  return

'-----------
.set_palette
'----------- (palidx)
  ptr = $40100 + palidx * 16 * 3

  for k = 0 to 15
    ' red
    poke $d100 + k + 16, peek(ptr)
    ptr = ptr + 1

    ' green
    poke $d200 + k + 16, peek(ptr)
    ptr = ptr + 1

    ' blue
    poke $d300 + k + 16, peek(ptr)
    ptr = ptr + 1
  next k
  return

'---------
.init_vars
'---------
  ' clear dma list area

  for k = 0 to 20
    poke $40000+k, 0
  next k

  poke $40000, $81 ' opt = dest addr MB selector
  poke $40001, $00 ' default to 1st megabyte

  poke $40002, $07 ' opt = enable transparency
  poke $40003, $86 ' opt = set transparency
  poke $40004, $20 ' transparency value
  poke $40005, $00 ' end of options
  poke $40006, $00 ' CMD lsb = COPY
  wpoke $40007, $0000 ' length
  wpoke $40009, $1000 ' source addr
  poke $4000b, $05    ' source bank
  wpoke $4000c, $1000 ' dest addr
  poke $4000e, $05    ' dest bank
  poke $4000f, $00    ' CMD msb (ignore)
  wpoke $40010, $0000 ' modulo (ignore)

  ' clear the offscreen buffer
  edma 3, $f00, 160, $50000
  edma 3, $a0, $20, $50f00
  edma 3, $fa0, $4f, $51000

  return

'-------------------
.init_petscii_frames
'-------------------
  max_frames = peek(data_addr)
  data_addr = data_addr + 1

  do while frm_idx < max_frames
    frame_addr(frm_idx) = data_addr
    data_addr = data_addr + 2
    frmw = peek(data_addr)
    data_addr = data_addr + 1
    frmh = peek(data_addr)
    data_addr = data_addr + 1
    data_addr = data_addr + frmw * frmh * 2

    ':print chr$(147);"frm_idx=";frm_idx

    frm_idx = frm_idx + 1
  loop
  return


'---------------------
.repair_petscii_frames
'---------------------
  frm_idx = 0

  do while frm_idx < max_frames
    data_addr = frame_addr(frm_idx)
    frmx = peek(data_addr)
    data_addr = data_addr + 1
    frmy = peek(data_addr)
    data_addr = data_addr + 1
    frmw = peek(data_addr)
    data_addr = data_addr + 1
    frmh = peek(data_addr)
    data_addr = data_addr + 1
    cdata_addr = data_addr + frmw * frmh

    frmx1=frmx+frmw-1
    frmy1=frmy+frmh-1

    for y = 0 to frmy1-frmy
      for x = 0 to frmx1-frmx
        if peek(data_addr + x + y * frmw) = 32 then begin
          poke(cdata_addr + x + y * frmw), $ff
        bend
      next x
    next y
    
    frm_idx = frm_idx + 1
  loop

  return

'-------------------------
.prepare_double_copy_layer
'-------------------------
  dblup_addr = $52000

  for frameidx = 0 to HIGHEST_LAYER
    data_addr = frame_addr(frameidx)
    frmx = peek(data_addr)
    data_addr = data_addr + 1
    frmy = peek(data_addr)
    data_addr = data_addr + 1
    frmw = peek(data_addr)
    data_addr = data_addr + 1
    frmh = peek(data_addr)
    data_addr = data_addr + 1

    frmx1=frmx+frmw-1
    frmy1=frmy+frmh-1

    frame_dblup_addr(frameidx) = dblup_addr

    ' COPY CHARS for each row twice into some location in dblup_addr
    for y = frmy to frmy1
      for k = 0 to 1
          ' copy it to some place in $5.2000
          edma 0, frmw, data_addr, dblup_addr
          dblup_addr = dblup_addr + frmw
      next k
      data_addr = data_addr + frmw
    next y

    ' COPY COLOR for each row twice into some location in dblup_addr
    for y = frmy to frmy1
      for k = 0 to 1
          ' copy it to some place in $5.2000
          edma 0, frmw, data_addr, dblup_addr
          dblup_addr = dblup_addr + frmw
      next k
      data_addr = data_addr + frmw
    next y

  next frameidx

  return

'---------------
.draw_background
'---------------
  getkey a$
  if a$="{x1D}" then gosub incr_offs ' xoffs=xoffs+1
  if a$="{x9D}" then gosub decr_offs ' xoffs=xoffs-1   
  if a$="," and palidx>0 then palidx=palidx-1:gosub set_palette
  if a$="." and palidx<11 then palidx=palidx+1:gosub set_palette

  if a$=" " and player_anim = ANIM_PWALK then begin
    player_anim = ANIM_PPUNCH
    player_frame = 0
  bend
  if a$="z" and player_anim = ANIM_PWALK then begin
    player_anim = ANIM_PKICK
    player_frame = 0
  bend

   '   frameidx = IMG_SUN
   '   gosub draw_petscii_layer 
   '   return

  poke $4000b, $05  ' assure source bank 5 (for doubled-up layers)

  for lidx = 0 to HIGHEST_LAYER
     ' if layer_visible[lidx] = 1 then begin
       frameidx = draw_order(lidx)
       gosub draw_petscii_layer 
     'bend
  next lidx

  gosub draw_player

  return


'-----------
.draw_player
'-----------
  frameidx = anim_order(player_anim, player_frame)
  fx=0 : fy=23
  gosub draw_petscii_frame 

  player_frame = player_frame + 1
  if player_frame >= MAX_PFRAME then begin
    player_anim = ANIM_PWALK
    player_frame = 0
  bend

  return

'------------------
.draw_petscii_frame
'------------------
  poke $4000b, $04  ' assure source bank 4 (for original frame data)

  data_addr = frame_addr(frameidx)
  frmx = peek(data_addr)
  data_addr = data_addr + 1
  frmy = peek(data_addr)
  data_addr = data_addr + 1
  frmw = peek(data_addr)
  data_addr = data_addr + 1
  frmh = peek(data_addr)
  data_addr = data_addr + 1

  frmx = fx ' override position
  frmy = fy

  frmx1=frmx+frmw-1
  frmy1=frmy+frmh-1

  scraddr = $50000 + frmx + frmy * 80
  clraddr = $51000 + frmx + frmy * 80
  cdata_addr = data_addr + frmw * frmh

  for yy=frmy to frmy1

    ' draw chars
    ' ----------
    src_addr = data_addr
    dest_addr = scraddr
    length = frmw
    transp = 32
    gosub transparent_dma

    src_addr = cdata_addr
    dest_addr = clraddr
    transp = $ff
    gosub transparent_dma

    data_addr = data_addr + frmw
    cdata_addr = cdata_addr + frmw
    scraddr = scraddr + 80
    clraddr = clraddr + 80  
  next yy
  return


'------------------
.draw_petscii_layer
'------------------
  data_addr = frame_addr(frameidx)
  frmx = peek(data_addr)
  data_addr = data_addr + 1
  frmy = peek(data_addr)
  data_addr = data_addr + 1
  frmw = peek(data_addr)
  data_addr = data_addr + 1
  frmh = peek(data_addr)
  data_addr = data_addr + 1

  frmx1=frmx+frmw-1
  frmy1=frmy+frmh-1

  scraddr = $50000 + frmx + frmy * 80
  clraddr = $51000 + frmx + frmy * 80
  cdata_addr = data_addr + frmw * frmh

  xoffs(frameidx) = mod(xoffs(frameidx) + 80, 80)
  xoffs = int(xoffs(frameidx))
  ' xoffs = mod(int(xoffs(frameidx))+80, 80)
  ' xoffs(frameidx) = xoffs

  data_addr = frame_dblup_addr(frameidx)
  cdata_addr = data_addr + frmw * frmh * 2

  for yy=frmy to frmy1

    ' draw chars from offset to end
    ' -----------------------------
    src_addr = data_addr + xoffs
    dest_addr = scraddr
    length = frmw
    transp = 32
    gosub transparent_dma

    src_addr = cdata_addr + xoffs
    dest_addr = clraddr
    transp = $ff
    gosub transparent_dma

    data_addr = data_addr + frmw * 2
    cdata_addr = cdata_addr + frmw * 2
    scraddr = scraddr + 80
    clraddr = clraddr + 80  
  next yy
  return


'--------
.gameplay
'--------
  gosub draw_background
  ' gosub draw_sprites
  gosub page_flip
  ' gosub user_input
  ' gosub game_logic
  goto gameplay
  return


'---------
.page_flip
'---------
  edma 0, $fa0, $50000, $40800
  edma 0, $fa0, $51000, $ff80000

  ' fill the top part
  edma 3, $f00, $4f, $51000
  return

'---------
.incr_offs
'---------
  for k = 0 to HIGHEST_LAYER
    xoffs(k) = xoffs(k) + offs_amount(k)
  next k
  return

'---------
.decr_offs
'---------
  for k = 0 to HIGHEST_LAYER
    xoffs(k) = xoffs(k) - offs_amount(k)
  next k
  return
ÿ